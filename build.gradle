/*
 * Copyright (c) 2016 Open Baton (http://www.openbaton.org)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
buildscript {
    repositories { mavenCentral() }
    dependencies { classpath("se.transmode.gradle:gradle-docker:1.2") }
}

plugins {
    id 'net.researchgate.release' version '2.5.0'
    id "nebula.ospackage" version "4.3.0"
    id 'com.github.sherter.google-java-format' version '0.5'
    // For the managing of the Licences
    id "com.github.hierynomus.license" version "0.14.0"
    // For managing the dependencies' versions
    id 'com.github.ben-manes.versions' version '0.13.0'
    id 'org.springframework.boot' version '1.5.0.RELEASE'
}

apply plugin: 'java'
apply plugin: 'docker'
apply plugin: 'idea'
apply plugin: 'eclipse'

apply from: 'gradle/gradle/git.helpers.gradle'
apply from: 'gradle/gradle/command.line.helper.gradle'

/**
 * Project settings
 */
ext {
    //------------------------------------------------//
    /**
     * Spring Related Variables
     */
    springVersion = '4.3.5.RELEASE'
    springBootVersion = '1.5.0.RELEASE'
    //------------------------------------------------//
    //------------------------------------------------//
    /**
     * Application Related Version
     */
    applicationVersion = version

    applicationRelease = isApplicationRelease()
    applicationDate = new Date()
    applicationRevision = getRevision()

    applicationVersionSnapshot = (!applicationRelease) ? ".${asUTC(applicationDate, 'yyyyMMdd')}.git${applicationRevision}" : ""
    applicationVersionFull = "${applicationVersion}${applicationVersionSnapshot}"
    //------------------------------------------------//
    //------------------------------------------------//
    /**
     * gradle Related Vars
     */
    //mainClass = 'org.openbaton.nfvo.main.Application'
    //------------------------------------------------//
    //------------------------------------------------//
    /**
     * Junit Version
     */
    junitVersion = "4.12"
}

apply from: 'gradle/gradle/application.java.gradle'
//apply from: 'gradle/gradle/run.application.java.gradle'
apply from: 'gradle/gradle/git.release.gradle'

apply from: 'gradle/gradle/packaging.nfvo.debian.gradle'
apply from: 'gradle/gradle/packaging.nfvo.docker.gradle'
apply from: 'gradle/gradle/packaging.innosetup.gradle'

apply from: 'gradle/gradle/distribution.gradle'

apply from: 'gradle/gradle/dependencies.updater.gradle'

apply from: 'gradle/gradle/brew.formula.creator.gradle'

repositories {
    mavenCentral()
}

downloadLicenses {
    ext.apacheTwo = license('Apache License, Version 2.0', 'http://opensource.org/licenses/Apache-2.0')
    ext.bsd = license('BSD License', 'http://www.opensource.org/licenses/bsd-license.php')
    ext.lgplTwoOne = license('LGPL 2.1','https://www.gnu.org/licenses/lgpl-2.1.html')
    ext.gplTwo = license('GPL v2','https://www.gnu.org/licenses/old-licenses/gpl-2.0.html')
    ext.mit = license('MIT','https://opensource.org/licenses/MIT')

    includeProjectDependencies = true

    aliases = [
            (apacheTwo) : ['The Apache Software License, Version 2.0', 'Apache 2', 'Apache License Version 2.0','Apache 2.0','Apache License, version 2.0', 'Apache License, Version 2.0', 'Apache License 2.0', license('Apache License', 'http://www.apache.org/licenses/LICENSE-2.0')],
            (bsd) : ['BSD', license('New BSD License', 'http://www.opensource.org/licenses/bsd-license.php')],
            (lgplTwoOne) : ['GNU Lesser General Public License 2.1', 'LGPL 2.1','GNU Lesser General Public License'],
            (gplTwo) : ['The GNU General Public License, Version 2', 'GPL v2'],
            (mit) : ['MIT', 'MIT License']
    ]

}
mainClassName = 'org.openbaton.nfvo.main.Application'


dependencies {
    compile project(':main')
}

allprojects {
    jar {
        manifest {
            attributes("Implementation-Version": version)
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}


compileJava.dependsOn tasks.googleJavaFormat
